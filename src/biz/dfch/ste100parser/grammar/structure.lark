// Copyright (C) 2026 Ronald Rink, d-fens GmbH, http://d-fens.ch
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU Affero General Public License as published
// by the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU Affero General Public License for more details.
//
// You should have received a copy of the GNU Affero General Public License
// along with this program.  If not, see <https://www.gnu.org/licenses/>.

// grammar.lark
// Inline markup grammar (strict; preserves whitespace; multiline quotes allowed)

?start: inline*

// Single line text elements.
?common_text: TEXT
    | APOSTROPHE
    | PLURAL_S
    | YEAR_SHORT
    | MULTIPLY

char_paren_open: PAREN_OPEN
char_paren_close: PAREN_CLOSE
char_star: STAR
char_under: UNDER
char_code: BACK_TICK
?char_single: char_paren_open
    | char_paren_close
    | char_star
    | char_under
    | char_code

// Single line pair-wise formatting containers.
?format_containers: bold
    | emph
    | bold_emph

// Single line pair-wise quoting containers.
?quote_containers: dquote
    | squote

// ---------- Top-level inline stream ----------
?inline: WS
    | NEWLINE
    | format_containers
    | code
    | quote_containers
    | cite
    | paren
    | paren_sl
    | proc
    | common_text

// ---------- Containers ----------

// Single line container.
dquote: DQUOTE dquote_item* DQUOTE
?dquote_item: WS
    | format_containers
    | code
    | squote
    | paren_sl
    | char_single
    | common_text

// Single line container.
squote: SQUOTE squote_item* SQUOTE
?squote_item: WS
    | format_containers
    | code
    | dquote
    | paren_sl
    | char_single
    | common_text

// Single line container.
bold: STAR_OPEN bold_item+ STAR_CLOSE
?bold_item: WS
    | emph
    | code
    | quote_containers
    | paren_sl
    | common_text

// Single line container.
emph: UNDER_OPEN emph_item+ UNDER_CLOSE
?emph_item: WS
    | bold
    | code
    | quote_containers
    | paren_sl
    | common_text

// Single line container.
bold_emph: BOLD_EMPH_OPEN bold_emph_item+ BOLD_EMPH_CLOSE
?bold_emph_item: WS
    | code
    | quote_containers
    | paren_sl
    | common_text

// Single line quote.
cite: cite_first_line cite_cont_line*
?cite_first_line: CITE_START_FIRST cite_item+
?cite_cont_line: CITE_START_CONT cite_item+
?cite_item: WS
    | format_containers
    | quote_containers
    | paren_sl
    | common_text

proc: proc_first_line proc_next_line*
?proc_first_line: PROCEDURE_NL_FIRST? proc_indent_prefix proc_marker proc_delimiter proc_indent_suffix procedure_item+
?proc_next_line: PROCEDURE_NL_CONT proc_indent_prefix proc_marker proc_delimiter proc_indent_suffix procedure_item+
?procedure_item: WS  
    | format_containers  
    | quote_containers  
    | paren_sl  
    | common_text

// We capture leading spaces as a token, so that we can discard them.
?proc_indent_prefix: " "*
proc_marker: /[a-zA-Z0-9]+/
proc_delimiter: /[.)]/
// We capture the trailing space as a token, so that we can discard it.
?proc_indent_suffix: " "

// ---------- Parentheses can be nested. ----------
// Multi line container. Use outside single line containers.
paren: PAREN_OPEN paren_item_ml* PAREN_CLOSE
?paren_item_ml: NEWLINE
    | paren_item_sl
// Single line container. Use it within single line containers.
paren_sl: PAREN_OPEN paren_item_sl* PAREN_CLOSE
?paren_item_sl: WS
    | format_containers
    | quote_containers
    | paren
    | common_text

// ---------- Code (literal until ending backtick) ----------
code: BACK_TICK CODE BACK_TICK

// ---------- Terminals ----------
// Highest priority: code and year abbreviations
CODE.40: /[^`]+/
YEAR_SHORT.40: /'\d{2}s?(?=[\s.,!?;:]|$)/

// Plural (s) - Matches (s) if preceded by a letter  
// and followed by space, punctuation, or end of string.  
// We use a lookbehind for a letter [A-Za-z]  
PLURAL_S.35: /(?<=[A-Za-z])\(s\)(?=[\s.,!?;:]|$)/

// Apostrophes (possessives, decades like 1990's)
// Matches ' or 's if preceded by alphanumeric
// and followed by space, punctuation, or end of string
APOSTROPHE.30: /(?<=[A-Za-z0-9])'(?:s)?(?=[\s.,!?;:]|$)/

// Make "*_" and "_*" single tokens so they don't split into STAR/UNDER
BOLD_EMPH_OPEN.10: "*_"
BOLD_EMPH_CLOSE.10: "_*"

CITE_START_FIRST.10: /(?:\A|\r?\n)> /
CITE_START_CONT.10: /\r?\n> /

PROCEDURE_NL_FIRST: /\r?\n/
PROCEDURE_NL_CONT:  /\r?\n/

// Delimiter tokens
PAREN_OPEN: "("
PAREN_CLOSE: ")"
DQUOTE: "\""
SQUOTE: "'"
MULTIPLY: " * "
STAR: "*"
STAR_OPEN.2: /\*(?!\s)/
STAR_CLOSE.2: /(?<!\s)\*/

UNDER: "_"
UNDER_OPEN.2: /_(?!\s)/
UNDER_CLOSE.2: /(?<!\s)_/

BACK_TICK: "`"

// Whitespace preserved.
// When there is more than one WS, then this is only one token.
WS: /[ \t]+/
// When there is more than on NEWLINE, then each NEWLINE is a different token.
NEWLINE: /\r?\n/

// Any run of characters that is not a delimiter or whitespace/newline
TEXT: /[^"'*_`()\s]+/

TEXT_NO_UNDER: /[^"'*`\s]+/